using System;
using System.Collections.Generic;
using IKVM.Reflection;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Sre.Macros;

namespace Nemerle.Compiler.Ikvm
{
  public sealed class FieldCustomAttributes : MemberCustomAttributes
  {
    public this([NotNull] fieldInfo : IKVM.Reflection.FieldInfo)
    {
      base(CustomAttributeData.GetCustomAttributes(fieldInfo));
      ObsoleteAttributes  = [];
      ImmutableAttribute  = None();
      IsVolatile          = None();
      foreach(attr in Data) attribute match(attr)
      {
        | System.ObsoleteAttribute() => // ctor with no arguments
          ObsoleteAttributes ::= (null, false)

        | System.ObsoleteAttribute(message : string) =>
          ObsoleteAttributes ::= (message, false)

        | System.ObsoleteAttribute(message : string, isError : bool) =>
          ObsoleteAttributes ::= (message, isError)
          
        | Nemerle.Internal.ImmutableAttribute =>
          ImmutableAttribute = Some(null)    

        | System.Runtime.CompilerServices.IsVolatile =>
          IsVolatile = Some(null)

        | Nemerle.Internal.VolatileModifier =>
          IsVolatile = Some(null)
      }
    }

    public ImmutableAttribute : option[object] { get }
    
    public IsVolatile : option[object] { get }
  }
}