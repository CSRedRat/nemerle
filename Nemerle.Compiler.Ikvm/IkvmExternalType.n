using System;
using Nemerle.Assertions;

using SCG = System.Collections.Generic;
using SR = IKVM.Reflection;

namespace Nemerle.Compiler.Sre
{
  public sealed class SreExternalType : ExternalType
  {
    public this([NotNull] library : LibraryReference, [NotNull] systemType : IKVM.Reflection.Type)
    {
      Library = library;
      SystemType = systemType;
      CustomAttributes = TypeCustomAttributes(systemType);
    }

    public Library : LibraryReference { get }

    public SystemType : IKVM.Reflection.Type { get }

    public CustomAttributes : TypeCustomAttributes { get }

    public override Assembly : string
    {
      get { SystemType.Assembly.ToString() }
    }

    public override FullName : string
    {
      get { SystemType.TypeFullName() }
    }

    public override GenericArgumentsCount : int
    {
      get { SystemType.GetGenericArguments().Length }
    }

    protected override ConstructTypeInfo(node : NamespaceTree.Node, fixNode : bool) : void
    {
      when (Type == null)
        Type = Library.ConstructTypeInfo(SystemType, node, CustomAttributes);
      when (fixNode)
        node.Value = NamespaceTree.TypeInfoCache.Cached(Type);
    }
  }
}