using Nemerle;
using Nemerle.Assertions;
using System;
using IKVM.Reflection;
using Nemerle.Collections;
using SRE = IKVM.Reflection.Emit;

namespace Nemerle.Compiler.Sre
{
  internal sealed class SreStaticTypeVar : StaticTypeVar
  {
    mutable _systemType : IKVM.Reflection.Type;

    public this(
      manager           : ManagerClass,
      name              : string,
      genericAttributes : System.Reflection.GenericParameterAttributes,
      systemType        : IKVM.Reflection.Type)
    {
      base(manager, name, genericAttributes);
      _systemType = systemType;
    }

    public GetSystemType() : IKVM.Reflection.Type
      requires _systemType != null
    {
      _systemType
    }

    public SetGenericBuilder([NotNull] builder : SRE.GenericTypeParameterBuilder) : void
      requires _systemType == null
    {
      builder.SetGenericParameterAttributes(SpecialConstraints :> GenericParameterAttributes);
      _systemType = builder;
    }

    public UpdateConstraints() : void
    {
      def builder = _systemType :> SRE.GenericTypeParameterBuilder;

      def ifaces =
        match (constraints) 
        {
          | (FixedType.Class as x) :: _ when x.tycon.IsInterface => constraints
          | x :: xs => builder.SetBaseTypeConstraint (x.GetSystemType ()); xs
          | [] => []
        };

      unless (ifaces.IsEmpty) 
        builder.SetInterfaceConstraints (ifaces.MapToArray(_.GetSystemType ()));
    }
  }
}