using System;
using System.Collections.Generic;
using System.Reflection;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Reflection.Macros;

namespace Nemerle.Compiler.Sre
{
  public sealed class MethodCustomAttributes : MemberCustomAttributes
  {
    public this([NotNull] methodInfo : MethodBase)
    {
      base(CustomAttributeData.GetCustomAttributes(methodInfo));
      ConditionalAttributes = [];
      ObsoleteAttributes    = [];
      ExtensionAttribute    = None();
      foreach(attr in Data) attribute match(attr)
      {
        | System.Diagnostics.ConditionalAttribute(conditionString : string) =>
          ConditionalAttributes ::= conditionString

        | System.ObsoleteAttribute() => // ctor with no arguments
          ObsoleteAttributes ::= (null, false)

        | System.ObsoleteAttribute(message : string) =>
          ObsoleteAttributes ::= (message, false)

        | System.ObsoleteAttribute(message : string, isError : bool) =>
          ObsoleteAttributes ::= (message, isError)

        | "System.Runtime.CompilerServices.ExtensionAttribute" => // requires System.Core.dll
          ExtensionAttribute = Some(null)

        | Nemerle.Internal.ExtensionAttribute =>
          ExtensionAttribute = Some(null)
      }
    }

    public ConditionalAttributes : list[string] { get }

    public ExtensionAttribute : option[object] { get }
  }
}