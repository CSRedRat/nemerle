/*
 * Copyright (c) 2003-2008 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using Nemerle.Compiler.Sre.Macros;
using Nemerle.IO;
using Nemerle.Imperative;
using Nemerle.Late;
using Nemerle.Utility;
using System.IO;
using System.Runtime.CompilerServices;

using PT = Nemerle.Compiler.Parsetree;
using SR = System.Reflection;
using SRE = System.Reflection.Emit;
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler.Sre
{
  /// This class stores information extracted from a referenced external library.
  public partial class LibraryReference : ICompilerContextBoundObject
  {
    public Manager : ManagerClass { get; }
    private InternalType : InternalTypeClass { get { Manager.InternalType } }
    //internal SystemTypeCache : SystemTypeClass { get { LibRefManager.SystemTypeCache } }

    /// The assembly associated with this object
    [Accessor (Assembly)] private _library : SR.Assembly;

    /// The location of this library
    private mutable _location : Location;

    [Accessor]
    private _isFriend : bool;

    /// If set to true, the current assembly declares itself
    /// to be generated by the Nemerle compiler.
    _is_generated_by_nemerle : bool;

    public LibRefManager : LibraryReferenceManager;

//    private static load_attr_from (assembly : SR.Assembly, attr_name : string, attr : ref System.Type, attr_loc : ref string) : void
//    {
//      def ext = assembly.GetType (attr_name);
//
//      when (ext != null && attr != null)
//      {
//        Message.Warning ($"$attr_name attribute from $assembly is ignored");
//        Message.HintOnce ($"$attr_name is first defined in $attr_loc");
//      }
//
//      def new_attr_loc = assembly.ToString();
//
//      when (ext != null && (attr == null || attr_loc == new_attr_loc))
//      {
//        attr = ext;
//        attr_loc = new_attr_loc;
//      }
//    }

    /// Load an assembly. Extracts and processes the custom attributes array.
    internal this (mgr : LibraryReferenceManager, assembly : SR.Assembly)
    {
      LibRefManager = mgr;
      Manager = mgr.Manager;
      _library = assembly;
      _location = Location (Location.GetFileIndex (mgr.getAssemblyLocation(assembly)), 0, 0);
      _location.MarkAsGenerated();

      // Scans the assembly custom attributes looking for something interesting...
      def attrs = _library.GetCustomAttributes (typeof (SR.AssemblyConfigurationAttribute), false);

      foreach (x :> SR.AssemblyConfigurationAttribute in attrs)
        _is_generated_by_nemerle ||= x.Configuration == "ContainsNemerleTypes";

//      load_attr_from (assembly, "Nemerle.Internal.ExtensionAttribute",
//                      ref SystemTypeCache.ExtensionAttribute,
//                      ref SystemTypeCache.ExtensionAttributeAssembly);
//      load_attr_from (assembly, "System.Runtime.CompilerServices.ExtensionAttribute",
//                      ref SystemTypeCache.SQ_ExtensionAttribute,
//                      ref SystemTypeCache.SQ_ExtensionAttributeAssembly);

      _isFriend = this.GetIsFriend();
    }

    private GetIsFriend() : bool
    {
      def currentAssemblyName = Path.GetFileNameWithoutExtension(this.Manager.Options.OutputFileName);
      def customAttributes = _library.GetCustomAttributes(typeof(InternalsVisibleToAttribute), false);
      def snKey() : SR.StrongNameKeyPair
      {
        using (keyFile = File.Open(this.Manager.Options.StrongAssemblyKeyName, FileMode.Open, FileAccess.Read))
          SR.StrongNameKeyPair(keyFile)
      }
      def toHexString(bytes : array[byte]) : string
      {
        def hex = System.BitConverter.ToString(bytes);
        hex.Replace("-","");
      }
      def generatePublicKeyToken(publicKey : array[byte]) : array[byte]
      {
        def sha = System.Security.Cryptography.SHA1Managed();
        def hash = sha.ComputeHash(publicKey);
        def len = 8;
        mutable result : array[byte] = array(len);
        System.Array.Copy(hash, hash.Length - len , result, 0, len);
        System.Array.Reverse(result, 0, len);
        result;
      }
      def checkFriend(att : InternalsVisibleToAttribute) : bool
      {
        def assemblyName = att.AssemblyName;
        def parts = assemblyName.Replace(" ", "").SplitToList(array[',']);
        match (parts)
        {
          | [asmName] when currentAssemblyName == asmName => true;
          | [asmName, pKey] when string.IsNullOrEmpty(this.Manager.Options.StrongAssemblyKeyName)
                                 && currentAssemblyName == asmName
            =>
            match(pKey.ToLower().SplitToList(array[',']))
            {
              | ["publickey", key] when toHexString(snKey().PublicKey).ToLower() == key.ToLower() => true;
              | ["publickeytoken", key]
                 when toHexString(generatePublicKeyToken(snKey().PublicKey)).ToLower() == key.ToLower()
                  => true;
              | _ => false
            }

          | _ => false
        }
      }

      customAttributes.Exists(a => checkFriend(a :> InternalsVisibleToAttribute))
    }

    public override ToString () : string
    {
      if (_location == Location.Default) Assembly.ToString()
      else if (Assembly != null) _location.ToString()
      else base.ToString()
    }

    /// Turns a Framework type into something edible by Nemerle's type system
    internal TypeOfType (_tenv : Map [string, StaticTypeVar], framework_type : System.Type) : FixedType
    {
      // Message.Debug (framework_type.ToString ());
      //if (framework_type.Equals(SystemTypeCache.Void))
      if(framework_type.FullName == "System.Void")
        InternalType.Void
      else if (framework_type.IsArray)
        FixedType.Array(TypeOfType(_tenv, framework_type.GetElementType()), framework_type.GetArrayRank())
      else if (framework_type.IsByRef)
      {
        Message.Error($ "ref type referenced $framework_type");
        TypeOfType(_tenv, framework_type.GetElementType())
      }
      else if (framework_type.IsGenericParameter)
      {
        match (_tenv.Find(framework_type.Name))
        {
          | Some(tv) => FixedType.StaticTypeVarRef(tv)
          | None =>
            // TODO: VladD2: Add autoload of referenced assembly
//                _tenv.Iter(fun(x,y) { Message.Debug($"$x - $y") });
            Util.ice($"$framework_type from $(framework_type.DeclaringType)")
        }
      }
      else
      {
        def tc = TypeInfoOfType(framework_type);
//            Message.Debug($"tinfo $tc contains generic framework_type.ToString());
        def parms =
          if (framework_type.IsGenericType)
            framework_type.GetGenericArguments().MapToList(t => TypeOfType(_tenv, t));
          else [];

        if (framework_type.Namespace == "Nemerle.Builtins")
        {
          def make_tupl(l)
          {
            | [x] => x
            | []  => InternalType.Void
            | _   => FixedType.Tuple(l)
          }

          if (tc.Name == "Tuple")
            FixedType.Tuple(parms)
          else if (tc.Name == "FunctionVoid")
            FixedType.Fun(make_tupl(parms), parms.Length, InternalType.Void)
          else
          {
            def (front, last) = parms.DivideLast();
            FixedType.Fun(make_tupl(front), front.Length, last)
          }
        }
        else FixedType.Class(tc, parms);
      }
    }

    mutable doEnsureCached : bool = false;

    // TODO: VladD2: Move this method into LibraryReferenceManager and make it independetnt from LibraryReference!
    internal TypeInfoOfType (framework_type : System.Type) : TypeInfo
    {
      def genericArgsCount = framework_type.GetGenericArguments().Length;
      //assert2(!(framework_type.Name == "Expression" && genericArgsCount > 0));
      def (ns_node, path_to_type) = LibRefManager.framework_nesting(framework_type);
      def ns_node_type = ns_node.Path (path_to_type);

      when (doEnsureCached)
        ns_node_type.EnsureCached();

      match (ns_node_type.LookupType ([], genericArgsCount))
      {
        | Some (tc) => tc
          // protected external types are not fetched automatically from external assembly
          // so sometimes we must load them by hand
        | None =>
          LibraryReferenceManager.GetInternalType (this, framework_type, ns_node_type)
      }
    }

    /// Construct TypeInfo object from given type in current assembly.
    internal ConstructTypeInfo (reflected_type : System.Type, ns_node : NamespaceTree.Node) : TypeInfo
    {
      def customAttributes = SR.CustomAttributeData.GetCustomAttributes(reflected_type);
      if(_is_generated_by_nemerle)
      {
        mutable extensionPatterns = [];
        mutable typeInfoCtor = DefaultNemerleTypeInfo(_, _, _, _);
        foreach(attrData in customAttributes) attribute match(attrData)
        {
          | Nemerle.Internal.VariantAttribute(variantOptions : string) =>
            typeInfoCtor = VariantNemerleTypeInfo(_, _, _, _, NString.Split(variantOptions, array [',']))

          | Nemerle.Internal.VariantOptionAttribute =>
            typeInfoCtor = VariantOptionNemerleTypeInfo(_, _, _, _)

          | Nemerle.Internal.ConstantVariantOptionAttribute =>
            typeInfoCtor = ConstantVariantOptionNemerleTypeInfo(_, _, _, _)

          | Nemerle.Internal.TypeAliasAttribute(aliasedTypeName : string) =>
            typeInfoCtor = TypeAliasNemerleTypeInfo(_, _, _, _, aliasedTypeName)

          | Nemerle.Internal.ExtensionPatternEncodingAttribute(name : string, ids : string, pattern : string) =>
            extensionPatterns ::= ExtensionPattern(
              parent      = this,
              identifiers = NString.Split(ids, [',']),
              pattern     = MainParser.ParseExpr (Manager.CoreEnv, pattern),
              name        = name)
        }
        def typeInfo = typeInfoCtor(this, reflected_type, nsNode, customAttributes);
        foreach(pattern in extensionPatterns)
          typeInfo.AddExtensionPattern(pattern);
        typeInfo
      }
      else if (reflected_type.IsPrimitive)
        ExternalPrimitiveTypeInfo(lib, reflected_type, nsNode, customAttributes)
      else
        ExternalTypeInfo(lib, reflected_type, nsNode, customAttributes)
    }

    /// Looks for type named [name] that is internal to current assembly. Used
    /// by type attribute decoder.
    internal LookupInternalType (name : string) : option [TypeInfo]
    {
    //  Message.Debug ($"looking `$(name)'");
      def st = _library.GetType (name);

      if (st == null)
        None ()
      else
      {
        def (ns_node, path_to_type) = LibRefManager.framework_nesting (st);
        def ns = ns_node.Path (path_to_type);
        Some (LibraryReferenceManager.GetInternalType (this, st, ns))
      }
    }

    private MethodOfMethodInfo (tenv : Map [string, StaticTypeVar], meth : SR.MethodInfo) : ExternalMethodInfo
    {
      if (meth == null) null
      else              ExternalMethodInfo(tenv, this, meth)
    }
  }
} /* namespace */
