using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
//using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Windows.Forms;
using Nemerle.Statechart;

namespace Nemerle.Statechart.Tests
{  

  [statechart(<#

  flags : auto_initial transition_completed_events debug;

  state FileWorking
  {
    close => Exiting;
    
    state Files
    {
      (H*)
      NewFile => Processing;
      Open => Opening;
      
      state Waiting
      {
          $> / SaveAllDis CaptionProgName;
          $< / SaveAsEna;
      }

      state Opened
      {
        save_as => Saving.SaveAs;
    
        state Saved
        {
            $> / SaveDis CaptionFileName LastFileCur;
            Save => @;
            Change => Changed;
        }

        state NotSaved
        {
            $> / SaveEna;
            
            state New
            {
                $> / CaptionFile LastFileEmpty SaveAsEna;
                Save => Saving.SaveAs;
            }

            state Changed
            {
                $> / CaptionChanged;
                Save => Saving.Overwrite;
            }
        }
      }
    }

    state Processing
    {
      _ => Saved;
      Cancel => Files.H;
    
      state CreateNew
      {
        _ => NotSaved;
        
      statemachine dialogs : FileDialogs
      {
        _ => NewDialog;
      }
      
        state NewDialog
        {
          $> / ShowNewDialog;
          ok / LoadData => $0;
        }
      
      }

        new statemachine Saving : FileDialogs
        {
          
          0 => {
                 in_state(NotSaved) => WantSave;
                 else => $0;
               }
    
          state WantSave
          {
            $> / ViewWantSave;
            yes => {
                     in_state(New) => SaveAs;
                     else => Overwrite;
                   }
            no => $0;
          }
    
          state SaveAs
          {
            $> / ViewSaveAs;
            ok => (save_junc);
          }
    
          state Overwrite
          {
            $> / ViewOverwrite;
            no => SaveAs;
            yes => (save_junc);
          }
    
          junction save_junc
          {
            / save => $0;
          }
        }
      
      state Opening
      {
        statemachine OpenSave : FileDialogs
        {
          _ => OpenDialog;
        }
      
        state OpenDialog
        {
          ok / LoadData => $0;
        }
      
      }
    
      state Exiting
      {
        statemachine ExitSave : FileDialogs
        {
          _ => Exit;
        }

        state Exit
        {
          0 => X;
          $> / Exit;
        }
        
      }
    }
  }
  #>
  )]
  public class FileFsm
  {
    
    
    LastFileCur() : void
    {
      LastSaved = CurFile;
    }

    LastFileEmpty() : void
    {
      LastSaved = "";
    }

    [Constructor]
    Init() : void
    {
      OpenDialog = OpenFileDialog();
      SaveDialog = SaveFileDialog();
      CurWantToSave = "Want to save file?";
      CurOverwrite = "Want to overwrite file?";
    }

    ViewOpenDialog() : DialogResult
    {
      OpenDialog.ShowDialog();
    }

    ViewSaveAsDialog() : DialogResult
    {
      SaveDialog.ShowDialog();
    }

    ViewNewDialog() : bool
    {
      if (NewDialog!= null) NewDialog(); else true
    }
    
    SaveCurrentFile() : bool
    {
      if (SaveFileAction!= null)
        SaveFileAction(CurFile) else true
    }

    ViewOverwrite() : DialogResult
    {
      //if (DisableDialogs) DialogResult.n
      MessageBox.Show(CurOverwrite, "Warning", MessageBoxButtons.YesNoCancel);
    }

    ViewWantToSave() : DialogResult
    {
      MessageBox.Show(CurWantToSave, "Warning", MessageBoxButtons.YesNoCancel);
    }

    SaveAsDialog() : bool
    {
      SaveDialog.FileName = CurFile;
      def res = ViewSaveAsDialog();
      if (res == DialogResult.OK)
      {
        CurFile = SaveDialog.FileName;
        SaveCurrentFile()
      }
      else false
    }

    DoSaveFile() : bool
    {
      if (IsNewFile)
      {
        SaveAsDialog();
      }
      else
      {
        match (ViewOverwrite())
        {
          | Yes => CurFile = LastSaved;SaveCurrentFile()
          | No => SaveAsDialog()
          | _ => false
        }
      }
    }

    public IsNewFile : bool
    {
      get
      {
        IsInState(State.New());
      }
    }

    public DisableDialogs : bool {get;set;}

    public OpenDialog : OpenFileDialog {get;set;}
    public SaveDialog : SaveFileDialog {get;set;}
    public NewDialog : void -> bool {get;set;}

    public LastSaved : string {get;private set;}
    public CurFile : string {get;private set;}
    public OpenFileAction : string -> bool {get;set;}
    public SaveFileAction : string -> bool {get;set;}
    public CurWantToSave : string {get;set;}
    public CurOverwrite : string {get;set;}


  }
}
