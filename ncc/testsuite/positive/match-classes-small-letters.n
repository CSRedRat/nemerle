using Nemerle;
using System;
using System.Console;

[Record]
class a
{
  public field : f;
}

[Record]
class f
{
  public field : int;
  public field2 : F;
}

[Record]
class F
{
  public field : int;
}

[Record]
variant b
{
  | A
  | B1
  | C1
  
  public field : int;
}

[Record]
variant B
{
  | A1
  | B2
  | C2
  
  public field : int;
}

module Test
{
  Main() : void
  {
    
    
      def check(mess, cond)
      {
        def str = if (cond()) "ok" else "fail";
        WriteLine($"$mess $str");
      }

      def test1(x)
      {
        | a(f) => check("Testing variable binding", () => f.field == 1)
      }

      def test2(x : object)
      {
        | t is a => check("Testing 'is' pattern", () => t.field.field == 1)
        | _      => WriteLine("failed")
      }

      def test3(x)
      {
        | b.A as t => check("Testing 'as' pattern 1", () => t.field == 3)
      }

      def test4(x)
      {
        | B as t => check("Testing 'as' pattern 2", () => t.field == 4)
      }

      def test5(x)
      {
        | a(f()) => check("Testing small letter type binding", () => true)
      }

      def test6(x)
      {
        | a(f(_, F)) => check("Testing small and high letter type binding", () => true)
      }

      def test7(x)
      {
        | a(f(1, F)) => check("Testing small and high letter type binding 2", () => true)
        | _          => WriteLine("failed")
      }

      def test8(x)
      {
        | a(f(1, F as t3)) => check("Testing small and high letter type binding with 'as'", () => t3.field == 2)
        | _                => WriteLine("failed")
      }

      def test9(x)
      {
        | a(field = f) => check("Testing variable binding 2", () => f.field == 1)
      }
      
      def test = a(f(1, F(2)));
      def test_2 = b.A(3);
      def test_3 = B.A1(4);
      
      test1(test);
      test2(test);
      test3(test_2);
      test4(test_3);
      test5(test);
      test6(test);
      test7(test);
      test8(test);
      test9(test);
  }
}

/*
BEGIN-OUTPUT
Testing variable binding ok
Testing 'is' pattern ok
Testing 'as' pattern 1 ok
Testing 'as' pattern 2 ok
Testing small letter type binding ok
Testing small and high letter type binding ok
Testing small and high letter type binding 2 ok
Testing small and high letter type binding with 'as' ok
Testing variable binding 2 ok
END-OUTPUT
*/
