using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Diagnostics;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{  
  
  public partial variant StateTransition
  {
    | Default
        {
          default : StateNode.Default;
          history : StateHistory;
          
          this(tran : Default, clone : bool = false, ncopy : int = 0)
          {
            base(tran, clone, ncopy);
            default = tran.default;
            history = tran.history;
          }
          
          this(st : FsmState, hist : StateHistory, default : StateNode.Default)
          {
            this.default = default;
            history = hist;
            action = default.action;
            From = st;
            Source = From;
            To = TransitionTarget.State(this, FsmState.State(default.target, true))
          }
        }
        
    | Initial 
        {
          initial : StateNode.Initial;
          
          this(tran : Initial, clone : bool = false, ncopy : int = 0)
          {
            base(tran, clone, ncopy);
            initial = tran.initial;
          }
          
          this(st : FsmState, init : StateNode.Initial)
          {
            From = st;
            Source = From;
            guard = init.guard;
            action = init.action;
            To =  TransitionTarget.Create(st, this, init.to);
          }
          
          this(from : FsmState, to : FsmState)
          {
            From = from;
            Source = From;
            To = TransitionTarget.State(this, to);
          }
          
        }
        
    [DebuggerDisplay("Transition  =  {Name}")]
    | Transition 
        {
          trigger : StateEvent;
          node : StateNode.Transition;
          
          TriggerName : string
          {
            get
            {
              trigger.Name
            }
          }

          static Create(st : FsmState, tr : list[StateNode.Transition]) : list[StateTransition]
          { 
            $[Transition(st, StateEvent(y), x) : StateTransition, x in tr, y in x.evt]
          }

          this(tran : Transition, clone : bool = false, ncopy : int = 0)
          {
            base(tran, clone, ncopy);
            trigger = tran.trigger;
            node = tran.node;
          }
          
          this(st : FsmState, evt : StateEvent, tr : StateNode.Transition)
          {
            From = st;
            Source = From;
            this.trigger = evt;
            node = tr;
            guard = tr.guard;
            action = tr.action;
            To = TransitionTarget.Create(st, this, tr.to)
          }

          this(st : FsmState, to : FsmState, source : PseudoStateNode)
          {
            From = st;
            Source = From;
            ImplicitFrom = source;
            To = TransitionTarget.State(this, to);
          }
          
          this(st : FsmState, trigger : StateEvent, to : TransitionTarget, source : PseudoStateNode)
          {
            From = st;
            this.trigger = trigger;
            Source = From;
            ImplicitFrom = source;
            To = to;
          }
          
          this(st : FsmState, tr : Transition)
          {
            this(tr);
            Source = st;
          }
        }
        
    | Internal
        {
          trigger : StateEvent;
          node : StateNode.InternalTransition;
          
          ActionNode : PathNode
          {
            get
            {
              PathNode.Action(action.Value)
            }
          }

          TriggerName : string
          {
            get
            {
              trigger.Name
            }
          }

          override ToString() : string
          {
            Name
          }

          this(tran : Internal, clone : bool = false, ncopy : int = 0)
          {
            base(tran, clone, ncopy);
            trigger = tran.trigger;
            node = tran.node;
          }
          
          this(st : FsmState, tr : Internal)
          {
            this(tr);
            Source = st;
          }
          
          this(trigger : StateEvent, tran : StateNode.InternalTransition, from : FsmState) 
          {
            this.trigger = trigger;
            node = tran;
            action = Some(node.action);
            guard = node.guard;
            From = from;
            Source = from;
          }

          static Create(st : FsmState, tr : list[StateNode.InternalTransition]) : list[StateTransition]
          {
              $[Internal(StateEvent(y), x, st) : StateTransition, x in tr, y in x.evt]
          }
          
        }
        
    | History
        {
          history : StateHistory;
          
          this(tran : History, clone : bool = false, ncopy : int = 0)
          {
            base(tran, clone, ncopy);
            history = tran.history;
          }
          
          this(hist : StateHistory, st : FsmState, to : FsmState)
          {
            From = st;
            history = hist;
            Source = From;
            To = TransitionTarget.State(this, to);
          }
        
        }
  }
  
  [Record]
  public variant DecisionNode
  {
    | Condition 
        {
          condition : GuardNode;
          body : DecisionNode;
          else_node : DecisionNode;
          
          this(c : Condition)
          {
            base(c);
            condition = c.condition;
            body      = c.body.Clone();
            else_node = c.else_node.Clone();
          }
        }
    | Action
        {
          condition : option[GuardNode];
          body : DecisionNode;
          
          this(c : DecisionNode.Action)
          {
            base(c);
            condition = c.condition;
            body      = c.body.Clone()
          }
        }
    | Else
        {
          body : DecisionNode;
          
          this(c : Else)
          {
            base(c);
            body = c.body.Clone()
          }
        }
    | Target 
        {
          target : TransitionTarget;
          [RecordIgnore] mutable destination : TransitionDestination;
          
          this(c : Target)
          {
            base(c);
            target = c.target.Clone()
          }
        }
      
    public this(node : DecisionNode)
    {
      level    = node.level;
      state    = node.state;
      actions  = node.actions;
    }
    
    public level : int;
        
    public state : FsmState;
    
    public actions : list[Actions];
        
    [RecordIgnore] public PrevState : FsmState {get;set;}
    
    public Clone() : DecisionNode
    {
      match (this)
      {
        | Condition as j => Condition(j)
        | Else as j => Else(j)
        | Target as j => Target(j)
        | Action as j => Action(j)
      }
    }

    public ActionNode : PathNode
    {
      get
      {
        PathNode.Sequence(actions |> PathNode.Action(_))
      }
    }
    
    public Actions : PathNode
    {
      get
      {
        match (this)
        {
          | Condition(body = b, else_node = null) => ActionNode + b.Actions
          | Condition(body = b, else_node = els) => ActionNode + b.Actions + els.Actions
          | Action(body = b)
          | Else(body = b) => ActionNode + b.Actions
          | Target => ActionNode
        }
      }
    }
    
    public CreatePath(func : DecisionNode -> PathNode * PathNode) : DecisionPath
    {
      def (n, o) = func(this);
      match (this)
      {
        | Condition(c, b, e) => 
            DecisionPath.Condition(level, state, actions, n, o, c, b.CreatePath(func), e.CreatePath(func))
        | Else(b) => 
            DecisionPath.Else(level, state, actions, n, o, b.CreatePath(func))
        | Action(c, b) => DecisionPath.Action(level, state, actions, n, o, c, b.CreatePath(func))
        | Target(t) as j => DecisionPath.Target(level, state, actions, n, o, t, j.destination)
      }
    }
    
    public Traverse(func : DecisionNode -> void) : void
    {
      def traverse(t)
      {
        func(t);
        match (t)
        {
          | Condition(body = b, else_node = null) => traverse(b)
          | Condition(body = b, else_node = els) => traverse(b);traverse(els)
          | Action(body = b)
          | Else(body = b) => traverse(b)
          | Target => ()
        }
      }
            
      traverse(this)
    }

            
  }
  
  [Record]
  public variant DecisionPath
  {
    | Condition 
        {
          condition : GuardNode;
          body : DecisionPath;
          else_node : DecisionPath;
          
        }
    | Else
        {
          body : DecisionPath;
        }
    | Action
        {
          condition : option[GuardNode];
          body : DecisionPath;
          
        }
    | Target 
        {
          target : TransitionTarget;
          destination : TransitionDestination;
    
        }
    
    public level : int;
        
    public state : FsmState;
            
    public actions : list[Actions];
        
    public Paths : PathNode
    {
      get
      {
        match (this)
        {
          | Condition(body = b, else_node = null) => Path + b.Paths
          | Condition(body = b, else_node = els) => Path + b.Paths + els.Paths
          | Action(body = b)
          | Else(body = b) => Path + b.Paths
          | Target(Path = p) => p
        }
      }
    }

    public ThisPath : PathNode
    {
      get
      {
        PathNode.Sequence(actions |> PathNode.Action(_))
      }
    }
    
    [Accessor] mutable node_path : PathNode = PathNode.Empty();
    [Accessor] mutable out_path : PathNode = PathNode.Empty();

    public ThisOutPath : PathNode
    {
      get
      {
        ThisPath + OutPath
      }
    }
   
    
    public Path : PathNode
    {
      get
      {
        NodePath + ThisPath + OutPath
      }
    }
          
    
    public Traverse(func : DecisionPath -> void) : void
    {
      def traverse(t)
      {
        func(t);
        match (t)
        {
          | Condition(body = b, else_node = null) => traverse(b)
          | Condition(body = b, else_node = els) => traverse(b);traverse(els)
          | Action(body = b)
          | Else(body = b) => traverse(b)
          | Target => ()
        }
      }
            
      traverse(this)
    }
    
  }
  
  public variant TransitionBodyKind
  {
    | Normal
    | Junction {tree : DecisionNode;}
    | Choice {tree : DecisionNode;}
  }
}
