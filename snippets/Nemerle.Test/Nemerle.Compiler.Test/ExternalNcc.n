using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Compiler.Test
{
  /// <summary>
  /// Description of ExternalNcc.
  /// </summary>
  public sealed class ExternalNcc : Ncc
  {
    public this(
                nccFileName             : string,
      [NotNull] nccArguments            : list[string],
      [NotNull] processStartInfoFactory : ProcessStartInfoFactory)
      requires !string.IsNullOrEmpty(nccFileName)
    {
      _nccFileName = nccFileName;
      _nccArguments = nccArguments;
      _processStartInfoFactory = processStartInfoFactory;
    }

    public override Compile([NotNull] arguments : list[string], files : list[string]) : NccResult
    {
      def actualArguments = List();
      actualArguments.AddRange(_nccArguments);
      actualArguments.AddRange(arguments);
      actualArguments.AddRange(files);

      def startInfo = _processStartInfoFactory.CreateProcessStartInfo(_nccFileName, actualArguments);
      startInfo.RedirectStandardOutput = true;
      startInfo.UseShellExecute = false;

      using(nccProcess = Process.Start(startInfo))
      {
        def compilerOutput = List();
        mutable line = nccProcess.StandardOutput.ReadLine();
        while(!ReferenceEquals(null, line))
        {
          compilerOutput.Add(line);
          line = nccProcess.StandardOutput.ReadLine();
        }

        unless (nccProcess.WaitForExit(20000) || nccProcess.HasExited)
          nccProcess.Kill();

        NccResult(nccProcess.ExitCode, compilerOutput.ToArray())
      }
    }

    private _nccFileName : string;
    private _nccArguments : list[string];
    private _processStartInfoFactory : ProcessStartInfoFactory;
  }
}
