using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Compiler.Test
{
  /// <summary>
  /// Description of ExternalNcc.
  /// </summary>
  public sealed class ExternalNcc : Ncc
  {
    public this([NotNull] runtime : ExternalNccRuntime)
    {
      _runtime = runtime;
    }

    public override Compile([NotNull] options : IEnumerable[string], files : IEnumerable[string]) : int * array[string]
    {
      def arguments = List();
      arguments.AddRange(_runtime.GetArguments());
      arguments.AddRange(options);
      arguments.AddRange(files);

      def startInfo = ProcessStartInfo();
      startInfo.FileName = _runtime.GetFileName();
      startInfo.Arguments = string.Join(" ", arguments);
      startInfo.RedirectStandardOutput = true;
      startInfo.UseShellExecute = false;

      using(nccProcess = Process.Start(startInfo))
      {
        def compilerOutput = List();
        mutable line = nccProcess.StandardOutput.ReadLine();
        while(!ReferenceEquals(null, line))
        {
          compilerOutput.Add(line);
          line = nccProcess.StandardOutput.ReadLine();
        }

        unless (nccProcess.WaitForExit(20000) || nccProcess.HasExited)
          nccProcess.Kill();

        (nccProcess.ExitCode, compilerOutput.ToArray())
      }
    }

    private _runtime : ExternalNccRuntime;
  }
}
