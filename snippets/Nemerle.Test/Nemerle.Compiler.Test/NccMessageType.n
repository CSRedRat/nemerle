using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Compiler.Test
{
  /// <summary>
  /// Description of NccMessageType.
  /// </summary>
  internal variant NccMessageType
  {
    | Unknown
    | Error   { line : int }
    | Warning { line : int }
    | Hint    { line : int }
    | GlobalError
    | GlobalWarning

    public static Parse(text : string) : NccMessageType
    {
      regexp match(text)
      {
        | @":(?<lineNumber : int>\d*):\d*:(\d*:\d*:)?\s+(?<messageTypeString>.*)$" =>
          regexp match(messageTypeString)
          {
            | "error:"   => Error(lineNumber)
            | "warning:" => Warning(lineNumber)
            | "hint:"    => Hint(lineNumber)
            | _          => Unknown()
          }
        | "error:"   => GlobalError()
        | "warning:" => GlobalWarning()
        | _          => Unknown()
      }
    }
  }
}
