using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nemerle.Test.Framework;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Compiler.Test
{
  internal sealed class NccTestOutputWriter
  {
    public this([NotNull] writer : ColorizedOutputWriter)
    {
      _writer = writer;
    }

    public HasErrors : bool { get { _errorBuffer.Count > 0 } }

    public Write(messageType : NccTestMessageType, message : string) : void
    {
      def color = match (messageType)
      {
        | Error => _errorBuffer.Add(message); ConsoleColor.Red
        | _ => null
      }
      _writer.Write(color, message);
    }

    public WriteLine(messageType : NccTestMessageType, message : string) : void
    {
      def color = match (messageType)
      {
        | Error => _errorBuffer.Add(message); ConsoleColor.Red;
        | _ => null
      }
      _writer.WriteLine(color, message);
    }

    public GetTestResult() : Result
    {
      if (_errorBuffer.Count > 0)
        Result.Fail(_errorBuffer.Last(), NList.ToList(_errorBuffer.Take(_errorBuffer.Count - 1)))
      else
        Result.Success("passed")
    }

    public Reset() : void
    {
      _errorBuffer.Clear();
    }

    private _writer : ColorizedOutputWriter;
    private _errorBuffer : List[string] = List();
  }

  internal enum NccTestMessageType
  {
    | Debug
    | Info
    | Error
  }
}
