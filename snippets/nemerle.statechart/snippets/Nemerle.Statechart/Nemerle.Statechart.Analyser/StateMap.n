using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using FsmState;
  
  public class StateMap
  {
    [Accessor]
    mutable map : list[FsmState];
        
    [Accessor]
    mutable errors : list[FsmError] = [];

    [Accessor] analyser : FsmAnalyser;
    
    public HaveErrors : bool
    {
      get
      {
         !errors.IsEmpty()
      }
    }

    public Count : int
    {
      get
      {
        map.Count()
      }
    }

    public Contains(state : string) : bool
    {
      map.Exists(x => FsmAnalyser.StrEq(x.Name, state))
    }

    public static GetFullName(st : list[FsmState]) : string
    {
      string.Join(".", st |> NativeName)
    }
    
    public Get(state : list[string]) : FsmState
    {
      def merge_name(n)
      {
        string.Join(".", n)
      }
      
      def get_loop(st, lst, path)
      {
        | (st, head :: tail, p) => 
           def path = head :: p;
           match (get_all_state(st, head), tail)
           {
             | ([], _) => errors ::= FsmError.StateNotFound(State(merge_name(path.Rev())));null
             | ([st], []) => st
             | ([st], _) => get_loop(st.SubStates, tail, path)
             | (lst, _) => errors ::= FsmError.SeveralState(lst |> State(merge_name((_.NativeName :: p).Rev())));null
           }
        | _ => null   
      }
      
      get_loop(map, state, [])
    }

    get_all_state(map : list[FsmState], state : string) : list[FsmState]
    {
      def name = state ?? FsmState.TopStateName;
      def (name, sub_name) = if (name != FsmState.TopStateName) 
                             {
                               def sub =  name.GetSub('[', ']');
                               if (sub != null) (name.GetSub('['), sub) else (name, null)
                             } else (name, null);
      def name = if (sub_name != null) $"$(name)SubRegion$sub_name" else name;
      map.FindAll(x => FsmAnalyser.StrEq(x.NativeName, name))
    }
    
    public Get(state : string) : FsmState
    {
      def st = state.SplitToList('.');
      Get(st)
    }
    
    public Add(st : FsmState) : void
    {
      map ::= st;
      st.FormatName();
      st.is_new = false;
    }
    
    public Rem(st : FsmState) : void
    {
      def st = get_all_state(map, st.NativeName);
      st.Iter(x => map = map.Remove(x))
    }
    
    public Item[index : string] : FsmState
    {
      get
      {
        Get(index)
      }
    }

    public this(analyser : FsmAnalyser)
    {
      this.analyser = analyser;
    }    
    
    public Init(topstate : FsmState) : void
    {
      def sub = topstate :: topstate.AllSubStates;
      sub.Iter(x => x.is_new = false);
      map = sub;
    }

    public Names : list[string]
    {
      get
      {
        map |> Name
      }
    }

    public States : list[FsmState]
    {
      get
      {
        map
      }
    }
  }
}
